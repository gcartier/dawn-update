;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Dawn-Update Product
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  See www.jazzscheme.org for details.


(module dawn-update.product jazz


(import (jazz.io)
        (jazz.product))


;;;
;;;; Build
;;;


(definition (build descriptor (unit: unit #f) (force?: force? #f))
  (define (resources-dir)
    (let ((bundle (product-descriptor-build-bundle descriptor)))
      (if bundle
          (new-directory~ {Directory Build} (format "{a}.app/Contents/Resources" bundle))
        {Directory Build})))
  
  (let ((project-dir {Directory Dawn-Update})
        (resources-dir (resources-dir)))
    (define (install-file path)
      (let ((src (new-file~ project-dir path))
            (dst (new-file~ resources-dir path)))
        (install-file~ src dst
          feedback: (lambda (action pathname)
                      (build-feedback "; {a} {a}..." action (get-name~ pathname))))))
    
    (define (install-directory path (src #f) (dst #f))
      (let ((src (new-directory~ project-dir (or src path)))
            (dst (new-directory~ resources-dir (or dst path))))
        (install-directory~ src dst
          feedback: (let ((first? #t))
                      (lambda (action pathname)
                        (when first?
                          (build-feedback "; {a} {a}..." action (get-name~ pathname))
                          (set! first? #f)))))))
    
    (define (copy-fonts)
      (install-directory #f '("jiri" "jazz" "etc" "fonts") '("etc" "fonts")))
    
    (define (copy-assets)
      (install-directory '("lib" "dawn-update" "assets")))
    
    (build-product-descriptor descriptor unit: unit force?: force?)
    (initialize-aliases)
    (copy-fonts)
    (copy-assets)))


;;;
;;;; Install
;;;


(cond-expand
  (mac
    (definition install-dir
      {Directory Home "Devel" "install"}))
  (else
    (definition install-dir
      {Directory Home "install"})))


(definition (install descriptor)
  (initialize-aliases)
  (install-application descriptor install-dir
    name: "dawn-update.app"
    platform?: #t
    git?: #t))


;;;
;;;; Register
;;;


(register-product 'dawn-update
  build: build
  install: install))
